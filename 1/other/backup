#!/usr/bin/guile
!#

(import (sph common) (only (ice-9 ftw) scandir))
(define (display-process-arguments . a) (display-line (string-join a " ")))

(define (create-result-backup-tarsnap sources dry-run)
  (every
    (l (source)
      (let (path-filelist (tmpnam))
        (call-with-output-file path-filelist
          (l (port-output)
            (process-chain-finished-successfully?
              (process-create-chain-with-pipes #f port-output
                (tail source) (thunk (port-copy-all (current-input-port) (current-output-port)))))))
        ( (if dry-run display-process-arguments execute) "tarsnap" "-cf"
          (source-names->destination-file-name (first source)) "-T" path-filelist)
        (delete-file path-filelist)))
    sources))

(define program-description "a backup file creator")
(define program-dependencies (q ("pack")))
(define max-bits-for-compression (expt 2 31))
(define path-config (string-append (getenv "HOME") "/.config/backup/"))
(define default-destination (getcwd))

(define (source-names->destination-file-name a)
  (string-append (if (string? a) a (string-join a "-")) "." (current-datetime-string)))

(define (create-full-destination-path source-names destination-path use-default-destination)
  (if destination-path
    (if (string-suffix? "/" destination-path)
      (string-append destination-path (source-names->destination-file-name source-names))
      destination-path)
    (string-append (if use-default-destination default-destination "")
      (source-names->destination-file-name source-names))))

(define (source->file-size-sum source) "procedure:thunk:writes-paths-to-standard-output -> integer"
  (string->number
    (string-trim-right (process-create-chain-with-pipes->string #f (tail source) "file-size-sum"))))

(define (pack-process-arguments source-name path dry-run encrypt size)
  (pairs "pack" "--read-paths=--"
    (compact
      (list
        (and path (string-append "--target=" (create-full-destination-path source-name path #t)))
        (and (> size max-bits-for-compression) "--compression") (and dry-run "--dry-run")
        (and encrypt "--encryption")))))

(define (pack-process-proc . a) (thunk (apply process-replace (apply pack-process-arguments a))))

(define create-result-backup-files
  (let
    (validate-destination-paths
      (l (a)
        (let
          (non-directories (filter (l (a) (catch #t (thunk (not (is-directory? a))) (const #t))) a))
          (if (null? non-directories) #t
            (error-create (q non-directory-destination-paths) non-directories)))))
    (l (sources destinations encrypt dry-run)
      (let
        ( (destination-paths
            (map ensure-trailing-slash
              (if destinations (map path->full-path destinations) (list (getcwd))))))
        (error-identity-if (validate-destination-paths destination-paths)
          (every
            (l (source)
              (let (size (source->file-size-sum source))
                (every
                  (l (destination-path)
                    (if dry-run
                      (display-process-arguments
                        (pack-process-arguments (first source) destination-path
                          dry-run encrypt size))
                      (process-chain-finished-successfully?
                        (process-create-chain-with-pipes #f #f
                          (tail source)
                          (pack-process-proc (first source) destination-path dry-run encrypt size)))))
                  destination-paths)))
            sources))))))

(define (name->source-path a) "string -> string" (string-append path-config a))

(define (get-source-names) "-> (string ...)/false"
  (false-if-exception
    (filter
      (l (e)
        (and (not (or (string-equal? e ".") (string-equal? e "..")))
          (let (stat-info (stat (string-append path-config e)))
            (and (eqv? (q regular) (stat:type stat-info))
              (not (= 0 (logand 73 (stat:perms stat-info))))))))
      (scandir path-config))))

(define (create-result-list-configs)
  (display-line (string-join (list-sort string<? (get-source-names)) " ")) #f)

(define (create-result-show-config source-names)
  (each (l (a) (display (file->string (name->source-path a)))) source-names) #f)

(define (create-result-list-files source-names)
  (each (l (a) (execute (name->source-path a))) source-names) #f)

(define (source-names->sources sources combine) "-> (list:proc-conf ...)"
  (if combine
    (list
      (pair sources
        (thunk (each (l (a) (execute->port (current-output-port) (name->source-path a))) sources))))
    (map (l (a) (pair a (thunk (execute->port (current-output-port) (name->source-path a)))))
      sources)))

(define create-result-backup
  (let
    (validate-source-names
      (l (a)
        (let (missing-source-names (complement a (get-source-names)))
          (if (null? missing-source-names) #t
            (error-create (q missing-source-names) missing-source-names)))))
    (l (combine destinations dry-run encrypt source-names tarsnap)
      (error-identity-if (validate-source-names source-names)
        (let (sources (source-names->sources source-names combine))
          (if tarsnap (create-result-backup-tarsnap sources dry-run)
            (create-result-backup-files sources destinations encrypt dry-run)))))))

(define*
  (backup #:key combine destinations dry-run encrypt list-configs list-files show-config sources
    tarsnap)
  (cond (list-files (create-result-list-files sources)) (list-configs (create-result-list-configs))
    (show-config (create-result-show-config sources))
    (else (create-result-backup combine destinations dry-run encrypt sources tarsnap))))

(define backup-cli
  (let
    ( (cli-sources (l (a) (string-split a #\,)))
      (command-line-interface
        (cli-create #:description program-description
          #:options
          (ql (dry-run) (tarsnap #:names #\t)
            (list-configs #:names #\l #:description "lists the recognised backup configurations")
            (list-files #:description "list files that would be included in the backup")
            (show-config #:description "display contents a configuration file")
            (combine #:description
              "combine all files corresponding to given configuration names and create one backup file")
            (encrypt #:names #\e) ((sources destinations ...) #:required? #f #:value-required? #t)))))
    (thunk
      (let
        (keyword-arguments
          (append-map
            (l (a)
              (let ((key (first a)) (value (tail a)))
                (list (symbol->keyword key) (case key ((sources) (cli-sources value)) (else value)))))
            (command-line-interface)))
        (error-pass-if (apply backup keyword-arguments) error-log-and-exit)))))

(backup-cli)
