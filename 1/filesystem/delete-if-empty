#!/usr/bin/guile
!#

(import (sph) (ice-9 ftw))

(define description
  "delete-if-empty :: path ...
   tries to delete all empty directories and empty files found among the given paths and directory trees. deletes deeper files first")

(define (file-system-for-each f . file-names)
  (for-each
    (l (file-name)
      (let
        ( (leaf (l (path stat result) (f path stat))) (up (l (path stat result) (f path stat)))
          (error
            (l (path stat errno result)
              (format (current-error-port) "warning: ~a: ~a~%" path (strerror errno)) #t))
          (true (const #t)))
        (file-system-fold true leaf true up true error #t file-name)))
    file-names))

(define (delete-if-empty paths)
  (apply file-system-for-each
    (l (path stat)
      (case (stat:type stat)
        ((regular) (and (= 0 (stat:size stat)) (delete-file path)))
        ((directory) (false-if-exception (rmdir path)))))
    paths))

(delete-if-empty (tail (program-arguments)))
